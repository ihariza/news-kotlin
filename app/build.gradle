plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.nhariza.news"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'NEWS_API', findProperty("NEWS_API_KEY") ?: ""
    }

    sourceSets {
        androidTest {
            java.srcDirs += ['src/testBuilders/java']
        }
        test {
            java.srcDirs += ['src/testBuilders/java']
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            testCoverageEnabled true
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding = true
    }

    flavorDimensions("flavorDimen")
    productFlavors {
        create("ins") {
            applicationIdSuffix = ".ins"
        }
        create("dev") {
            applicationIdSuffix = ".dev"
        }
        create("pre") {
            applicationIdSuffix = ".pre"
        }
        create("pro") { }
    }

    testOptions {
        animationsDisabled = true
    }
}

dependencies {

    def room_version = "2.2.6"
    def retrofit_version = "2.9.0"
    def glide_version = "4.12.0"
    def koin_version = "2.2.2"
    def navigation_version = "2.3.4"
    def espresso_version = "3.3.0"

    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation 'androidx.fragment:fragment-ktx:1.3.2'

    // navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    // retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.8.0'

    // room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // koin
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"

    // glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation "com.github.bumptech.glide:recyclerview-integration:$glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation "androidx.room:room-testing:$room_version"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.3'
    testImplementation "io.mockk:mockk:1.11.0"

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_version"
    androidTestImplementation 'com.google.code.gson:gson:2.8.5'
    androidTestImplementation "io.mockk:mockk-android:1.11.0"
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:4.9.0'
    androidTestImplementation 'com.jakewharton.espresso:okhttp3-idling-resource:1.0.0'
    androidTestImplementation "org.koin:koin-test:$koin_version"
    androidTestImplementation('com.schibsted.spain:barista:3.9.0') {
        exclude group: 'org.jetbrains.kotlin'
    }

    debugImplementation 'androidx.fragment:fragment-testing:1.3.2'
}